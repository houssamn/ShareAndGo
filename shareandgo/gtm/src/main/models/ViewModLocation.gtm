Window ViewModLocation {
    Location location
    Location locToModify

    Label Loc_Name_L {
        String text := ['Location name']
    }   

    TextField Loc_Name_TF {
        String text := [$ViewModLocation.location$.name]
    }

    Label CoordX_L {
        String text := ['Coordinate X']
    }

    TextField CoordX_TF {
        String text := [$ViewModLocation.location$.x.toString()]
    }

    Label CoordY_L {
        String text := ['Coordinate Y']
    }

    TextField CoordY_TF {
        String text := [$ViewModLocation.location$.y.toString()]
    }

    Label Zones_L {
        String text := ['Available Zones']
    }

    ComboBox ZoneCB {
        Set(Zone) rows := [Zone.allInstances()]
        Zone selected := [null]

        Label zoneName {
            String text := [$ViewModLocation.ZoneCB.row$.zname]
        }
    }

    Button Add_Zone_B {
        String text:= ['Add Location to Zone']
        event onClick {
            if [not $ViewModLocation.ZoneCB.selected$.oclIsUndefined()] {
                try {
                    [$ViewModLocation.locToModify$.zones] += [$ViewModLocation.ZoneCB.selected$]
                    reevaluate ViewModLocation.ParentZones.rows
                }catch(SecurityException){
                    notification(['Message'], ['You Are Not Allowed To Add This Zone'], [500])
                }
            }else {
               notification(['Message'], ['Please Select A Zone'], [500])
            }
        }

    }

    Label Zones_Table_L {
        String text := ['Selected Zones']
    }

    Table ParentZones {
        Set(Zone) rows := [$ViewModLocation.locToModify$.zones]
        Zone selected := [null]
        columns {
            ['Zone']  : Label zoneName {
                String text
                event onView (text) {
                    try {
                        text := [$ViewModLocation.ParentZones.row$.zname]
                    }
                    catch(SecurityException) {
                        text := ['****']
                    }
                }
            }
        }
    }

    Button modify_location_B {
        String text := ['Submit changes']
        event onClick {
            if [$ViewModLocation.locToModify$.zones->size() < 1] {
                notification([''],['Error. At least one zone should be selected'],[500])
            } else {
                if [not $ViewModLocation.Loc_Name_TF.text$.oclIsUndefined()] {
                    [$ViewModLocation.location$.name] :=  [$ViewModLocation.Loc_Name_TF.text$]
                }
                if [not $ViewModLocation.CoordX_TF.text$.oclIsUndefined()] {
                    [$ViewModLocation.location$.x] :=  [$ViewModLocation.CoordX_TF.text$.toReal()]
                }
                if [not $ViewModLocation.CoordY_TF.text$.oclIsUndefined()] {
                    [$ViewModLocation.location$.y] :=  [$ViewModLocation.CoordY_TF.text$.toReal()]
                }

                [$ViewModLocation.location$.zones] :=  [$ViewModLocation.locToModify$.zones]

                delete [$ViewModLocation.locToModify$]
                back
            }
        }
    }

    Button Back_B {
            String text := ['Back']
            event onClick {
                delete [$ViewModLocation.locToModify$]
                back
            }
    }
} 
