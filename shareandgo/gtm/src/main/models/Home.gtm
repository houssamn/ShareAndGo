Window Home {
    Person caller
    String text := ['Home']

    Label Homepage_L {
        String text := ['Welcome To Your Homepage']
    }   

    Label Banned_L {
        String text
        event onView(text) {
            if [$Home.caller$.myRole  = Role::BANNED_USER] {
                text := ['!! YOU ARE BANNED !! Get Out !!']
            }
        }
    }

    //Acces To User's Personal Info In Addition to Other Users' Names 
    Button ViewUsers_B {
        String text := ['View User Info']
        event onClick {
            open UserInfo(caller:[$Home.caller$], role:[$Home.caller$.myRole])
        }
    }


    //Access to Zone-Location Information
    Button ViewZones_B {
        String text := ['View Zone Information']
        event onClick  {
            open ViewZones(caller:[$Home.caller$] , role: [$Home.caller$.myRole])
        }
    }


    //View Trips Button
    Button ViewTrips_B {
        String text := ['View Trips']
        event onClick {
            //Opens Trips Viewer in a separate Page
            open ViewTrips(caller:[$Home.caller$], role:[$Home.caller$.myRole])
        }
    }

    //Create Trips Button
    Button CreateTrip_B {
        String text := ['Create Trip']
        
        event onClick {
            //Open Create Trip Window
            if[$Home.caller$.myRole = Role::PREMIUM_USER]{
                try{
                    t := new Trip
                    [$t$.isCreated] := [false]
                    [$t$.driver] := [$Home.caller$]
                    open CreateTrip(caller:[$Home.caller$], role:[$Home.caller$.myRole], tripToCreate:[$t$])
                }catch(SecurityException){
                    notification (['Message'], ['You Cannot Do That'], [500])
                }   
            }else{
                notification (['Message'], ['Only A Premium User Can Create Trips'], [1000])
            }
        }

    }

    Button Report_B {
        String text := ['Report Misbehaviour']
        event onClick {
            try{ 
                cmpl := new Complaint
                [$cmpl$.isCreated] := [false]
                [$cmpl$.complaintOwner] := [$Home.caller$]
                open Report(caller:[$Home.caller$], role:[$Home.caller$.myRole], complaintToCreate:[$cmpl$])
            }catch(SecurityException){
                notification(['Message'], ['You cannot do that .. Sorry bro '], [500])
            }
        }
    }
}